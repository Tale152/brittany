/*
   Project greenhouse_core

   Gradle build file for JaCaMo Applications
   December 09, 2021 - 17:52:44
*/

defaultTasks 'run'

apply plugin: 'java'
apply plugin: 'eclipse'

version '1.0'
group   'org.jacamo'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "http://jacamo.sourceforge.net/maven2" }
    maven { url "https://raw.github.com/jacamo-lang/mvn-repo/master" }
    maven { url "https://repo.gradle.org/gradle/libs-releases-local" }

    maven { url "https://git.informatik.uni-hamburg.de/api/v4/groups/sane-public/-/packages/maven" }

    //maven { url "https://jade.tilab.com/maven/" }

    flatDir {
       dirs 'lib'
    }

    mavenCentral()
}

dependencies {
    compile group: 'org.jacamo', name: 'jacamo', version: '0.9'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.0'
    compile 'com.google.code.gson:gson:2.8.9'
    compile group: 'city.sane.wot-servient', name: 'wot-servient', version: '1.15'
    compile group: 'city.sane.wot-servient', name: 'wot-servient-binding-http', version: '1.15'
}

sourceSets {
    main {
        java {
            srcDir 'src/env'
            srcDir 'src/agt'
            srcDir 'src/org'
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

task run (type: JavaExec, dependsOn: 'classes') {
    group       ' JaCaMo'
    description 'runs the JaCaMo application'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'greenhouse_core.jcm'
    classpath sourceSets.main.runtimeClasspath
    environment 'AUTH_SERVICE_URL', 'https://brittany-auth-service.herokuapp.com:443/agent/login'
    environment 'PERSISTENCE_SERVICE_URL', 'https://brittany-persistence-service.herokuapp.com:443/'
    environment 'SETTINGS_SERVICE_URL', 'https://brittany-settings-service.herokuapp.com:443/latest'

    if (project.hasProperty("organization") && project.hasProperty("greenhouse") &&
        project.hasProperty("environment") && project.hasProperty("password")) {
        environment 'ORGANIZATION_NAME', project.getProperty("organization")
        environment 'GREENHOUSE_NAME', project.getProperty("greenhouse")
        environment 'ENVIRONMENT_NAME', project.getProperty("environment")
        environment 'ENVIRONMENT_PASSWORD', project.getProperty("password")
    }
}


task uberJar(type: Jar, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'creates a single runnable jar file with all dependencies'
    manifest {
        attributes 'Main-Class': 'jacamo.infra.JaCaMoLauncher'
    }
    baseName = 'jacamo-greenhouse_core' // the name must start with jacamo so that jacamo...jar is found in the classpath
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from (project.projectDir.absolutePath) {
        include '**/*.asl'
        include '**/*.xml'
        include '**/*.jcm'
        include '*.properties'
    }
    from (project.buildDir.absolutePath + '/jcm') {
        include '**/*'
    }
    with jar

    doFirst {
        copy {
            from 'greenhouse_core.jcm'
            rename 'greenhouse_core.jcm','default.jcm'
            into project.buildDir.absolutePath + '/jcm'
        }
    }
}

clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}

task buildDockerImage(type: Exec, dependsOn: uberJar){
    commandLine("docker", "image", "build", ".", "-t", "greenhouse-core")
}

task dockerUp(type: Exec, dependsOn: buildDockerImage){
    if (project.hasProperty("organization") && project.hasProperty("greenhouse") &&
        project.hasProperty("environment") && project.hasProperty("password")) {
        def organizationEnv = "ORGANIZATION_NAME=" + project.getProperty("organization")
        def greenhouseEnv = "GREENHOUSE_NAME=" + project.getProperty("greenhouse")
        def environmentEnv = "ENVIRONMENT_NAME=" + project.getProperty("environment")
        def passwordEnv = "ENVIRONMENT_PASSWORD=" + project.getProperty("password")
        commandLine("docker", "container", "run",
                    "-e", organizationEnv,
                    "-e", greenhouseEnv,
                    "-e", environmentEnv,
                    "-e", passwordEnv,
                    "-d", "--rm", "--name", "greenhouse-core-container", "greenhouse-core")
    }
}

task dockerLog(type: Exec){
    commandLine("docker", "logs", "-f", "greenhouse-core-container")
}

task dockerDown(type: Exec){
    commandLine("docker", "container", "stop", "greenhouse-core-container")
}
